import si.kamino.gradle.extensions.version.code.IncreaseVersionCode

buildscript {
    ext {
        kotlinVersion = '1.2.21'
    }

    repositories {
        google()
        maven { url "https://plugins.gradle.org/m2/" }
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.0-beta2'
        classpath 'com.jakewharton.hugo:hugo-plugin:1.2.1'
        classpath 'com.github.dcendents:android-maven-plugin:1.2'
        classpath "gradle.plugin.si.kamino.gradle:android-version:1.5.1"
        classpath 'com.jakewharton:butterknife-gradle-plugin:8.5.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

ext {
    appCompatVersion = "27.0.2"
    targetSdk = 27
    compileSdk = 27
    minSdk = 16
    butterknifeVersion = "8.8.1"
    okHttpVersion = "3.4.2"
    daggerVersion = "2.14.1"
    javaxAnnotationVersion = "1.2"
    torrentstreamVersion = '2.5.0'
    filePickerVersion = '1.1.0'
    changeloglibVersion = '2.1.0'
    gsonVersion = '2.8.1'
    rxJavaVersion = '2.1.8'
}

allprojects {
    repositories {
        jcenter()
        google()
        maven { url 'https://maven.fabric.io/public' }
        maven { url 'http://guardian.github.com/maven/repo-releases' }
        maven { url "https://dl.bintray.com/butterproject/maven/" }
    }

    //let project evaluate its script first
    project.afterEvaluate {
        //only apply to projects that have the android plugin
        if (project.plugins.hasPlugin('com.android.application')) {

            project.androidVersion {
                appVersion {
                    versionCode {
                        digits 2
                    }
                }

                splits {
                    abi {
                        "armeabi-v7a" {
                            versionCode(IncreaseVersionCode) {
                                add 2000000
                            }
                        }

                        "arm64-v8a" {
                            versionCode(IncreaseVersionCode) {
                                add 3000000
                            }
                        }

                        x86 {
                            versionCode(IncreaseVersionCode) {
                                add 4000000
                            }
                        }

                        x86_64 {
                            versionCode(IncreaseVersionCode) {
                                add 5000000
                            }
                        }
                    }
                }
            }
        }
    }
}
