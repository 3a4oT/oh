apply from: "${project.rootDir}/config/gradle/android-library-config.gradle"
apply plugin: 'com.jakewharton.butterknife'

android {
    def buildNumber = (System.getenv('BUILD_NUMBER') == null ? 0 : System.getenv('BUILD_NUMBER')) as int
    def gitBranch = (System.getenv('GIT_BRANCH') == null ? 'local' : System.getenv('GIT_BRANCH').replace('origin/', ''))
    def releaseType = (System.getenv('RELEASE_TYPE') == null ? 'NONE' : System.getenv('RELEASE_TYPE'))
    def updateUrls = (System.getenv('UPDATE_URLS') == null ? '"http://localhost/android-update.json"' : System.getenv('UPDATE_URLS'))

    defaultConfig {
        minSdkVersion rootProject.ext.minSdk
        targetSdkVersion rootProject.ext.targetSdk
        buildConfigField 'Integer', 'BUILD_NUMBER', '' + buildNumber + ''
        buildConfigField 'String', 'GIT_BRANCH', '\"' + gitBranch + '\"'
        buildConfigField 'String', 'RELEASE_TYPE', '\"' + releaseType + '\"'
        buildConfigField 'String[]', 'UPDATE_URLS', '{' + updateUrls + '}'
        versionCode buildNumber
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
        lintConfig file('lint.xml')
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:multidex:1.0.1'
    api "com.android.support:support-v4:${rootProject.ext.appCompatVersion}"
    api "com.android.support:palette-v7:${rootProject.ext.appCompatVersion}"
    api "com.android.support:appcompat-v7:${rootProject.ext.appCompatVersion}"
    api "com.android.support:design:${rootProject.ext.appCompatVersion}"

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.3.1'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.3.1'

    api 'com.jakewharton.timber:timber:4.5.1'
    api "com.squareup.okhttp3:okhttp:${rootProject.ext.okHttpVersion}"
    api 'com.squareup.picasso:picasso:2.5.2'
    api 'com.jakewharton.picasso:picasso2-okhttp3-downloader:1.1.0'
    api 'com.google.code.gson:gson:2.8.0'
    api "com.jakewharton:butterknife:${rootProject.ext.butterknifeVersion}"
    api "com.github.gabrielemariotti.changeloglib:changelog:${changeloglibVersion}"
    api 'com.jakewharton.threetenabp:threetenabp:1.0.5'
    api 'com.koushikdutta.async:androidasync:2.1.9'

    api "com.github.se-bastiaan:TorrentStream-Android:${torrentstreamVersion}"

    implementation 'de.timroes:aXMLRPC:1.8.1'
    api "com.google.dagger:dagger:${rootProject.ext.daggerVersion}"

    api 'com.butterproject:vlc-android-sdk:2.0.6'
    api project(':connectsdk')

    annotationProcessor "com.google.dagger:dagger-compiler:${rootProject.ext.daggerVersion}"
    annotationProcessor "com.jakewharton:butterknife-compiler:${rootProject.ext.butterknifeVersion}"

    compileOnly "javax.annotation:javax.annotation-api:${rootProject.ext.javaxAnnotationVersion}"
}
